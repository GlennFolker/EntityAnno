plugins{
    id 'java-gradle-plugin'
    id 'maven-publish'
}

sourceSets.main.java.srcDirs = ['src/']

gradlePlugin{
    plugins{
        entityAnno{
            id = 'com.github.GlennFolker.EntityAnno'
            displayName = 'EntityAnno'
            implementationClass = 'ent.EntityAnnoPlugin'
        }
    }
}

configure(allprojects){
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'com.github.GlennFolker'
    version = '1.0.0'

    ext{
        compilerVersion = JavaVersion.current().ordinal() - JavaVersion.VERSION_17.ordinal() + 17

        arc = {String module ->
            return "com.github.Anuken.Arc$module:$arcVersion"
        }

        mindustry = {String module ->
            return "com.github.Anuken.MindustryJitpack$module:$mindustryVersion"
        }
    }

    repositories{
        google()
        mavenCentral()
        maven{url 'https://oss.sonatype.org/content/repositories/snapshots/'}
        maven{url 'https://oss.sonatype.org/content/repositories/releases/'}
        maven{url 'https://jitpack.io'}
    }

    java{
        withJavadocJar()
        withSourcesJar()
        toolchain{
            languageVersion = JavaLanguageVersion.of(compilerVersion)
        }
    }

    artifacts{
        archives tasks.sourcesJar
    }

    tasks.withType(Javadoc).configureEach{
        options{
            encoding = 'UTF-8'

            addStringOption('Xdoclint:none', '-quiet')
            addMultilineStringsOption('-add-exports').setValue((project['org.gradle.jvmargs'] as String)
                .split('\\s+')
                .findAll{it.startsWith('--add-opens')}
                .collect{it.substring('--add-opens='.length())}
            )
        }
    }

    sourceCompatibility = compilerVersion
    tasks.withType(JavaCompile).configureEach{
        options.incremental = true
        options.encoding = 'UTF-8'

        sourceCompatibility = compilerVersion
        targetCompatibility = 8

        doFirst{
            sourceCompatibility = 8
        }
    }
}

configure([project(':downgrader'), project(':entity')]){
    publishing{
        publications{
            maven(MavenPublication){
                from components.java
            }
        }
    }
}

dependencies{
    annotationProcessor project(':downgrader')

    implementation arc(':arc-core')
    implementation "org.jetbrains.kotlin.jvm:org.jetbrains.kotlin.jvm.gradle.plugin:$kotlinVersion"
    implementation "org.jetbrains.kotlin.kapt:org.jetbrains.kotlin.kapt.gradle.plugin:$kotlinVersion"
}
